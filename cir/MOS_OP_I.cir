.IF (select = 0)
    V1  2 0 dc 0 ac 1 0
    V2  s2 0 dc {VS}
    V3  d2 0 dc {VD}
    V4  b2 0 dc {VB}
.ELSEIF (select = 1)
    V1  2 0 dc 0
    V2  s2 0 dc {VS} ac 1 0
    V3  d2 0 dc {VD}
    V4  b2 0 dc {VB}
.ELSEIF (select = 2)
    V1  2 0 dc 0
    V2  s2 0 dc {VS}
    V3  d2 0 dc {VD} ac 1 0
    V4  b2 0 dc {VB}
.ELSEIF (select = 3)
    V1  2 0 dc 0
    V2  s2 0 dc {VS}
    V3  d2 0 dc {VD} 
    V4  b2 0 dc {VB} ac 1 0
.ENDIF

.ac lin 1 {freq} {freq}

.options filetype=ascii

.csparam freq   = {freq}
.csparam start  = {start}
.csparam delta  = {delta}
.csparam num    = {num}

.control

let ID     = start
let Cgg    = vector(num) ; total capacitance at the gate
let Cgs    = vector(num) ; gate-source capacitance
let Cgd    = vector(num) ; gate-drain capacitance
let Cgb    = vector(num) ; gate-bulk capacitance
let Css    = vector(num) ; total capacitance at the source
let Csg    = vector(num) ; source-gate capacitance
let Csb    = vector(num) ; source-bulk capacitance
let Csd    = vector(num) ; source-drain capacitance
let Cdd    = vector(num) ; total capacitance at the drain
let Cdg    = vector(num) ; drain-gate capacitance
let Cdb    = vector(num) ; drain-bulk capacitance
let Cds    = vector(num) ; drain-source capacitance
let Cbb    = vector(num) ; total capacitance at the bulk
let Cbs    = vector(num) ; bulk-source capacitance
let Cbd    = vector(num) ; bulk-drain capacitance
let Cbg    = vector(num) ; bulk-gate capacitance
let Ggg    = vector(num) ; total gate conductance
let Ggs    = vector(num) ; gate-source conductance
let Ggd    = vector(num) ; gate-drain conductance
let Ggb    = vector(num) ; gate-bulk conductance
let Gsg    = vector(num) ; source gate conductance
let Gss    = vector(num) ; total source conductance
let Gsd    = vector(num) ; source-drain conductance
let Gsb    = vector(num) ; source-bulk conductance
let Gdg    = vector(num) ; drain-gate conductance
let Gds    = vector(num) ; drain-source conductance
let Gdd    = vector(num) ; total drain conductance
let Gdb    = vector(num) ; drain-bulk conductance
let Gbg    = vector(num) ; bulk-gate conductance
let Gbs    = vector(num) ; bulk-source conductance
let Gbd    = vector(num) ; bulk-drain conductance
let Gbb    = vector(num) ; total bulk conductance
let vgs    = vector(num) ; gate-source operating voltage
let ids    = vector(num) ; drain-source operating current
let idx    = 0

alterparam select = 0
reset

repeat $&num
  alter I1 $&ID
  run
  let Cgg[idx]  = -imag(V1#branch)/(2*PI*freq)
  let Cgs[idx]  = imag(V2#branch)/(2*PI*freq)
  let Cgd[idx]  = imag(V3#branch)/(2*PI*freq)
  let Cgb[idx]  = imag(V4#branch)/(2*PI*freq)
  let Ggg[idx]  = -real(V1#branch)
  let Ggs[idx]  = real(V2#branch)
  let Ggd[idx]  = real(V3#branch)
  let Ggb[idx]  = real(V4#branch)
  let vgs[idx]  = @M1[vgs]
  let ID = ID + delta
  let idx = idx + 1
end

let ID     = start
let idx    = 0

repeat $&num
  alter I1 $&ID
  op
  let ids[idx] = @M1[id]
  let ID = ID + delta
  let idx = idx + 1
end

alterparam select = 1
reset

let ID     = start
let idx    = 0

repeat $&num
  alter I1 $&ID
  run
  let Css[idx]  = -imag(V2#branch)/(2*PI*freq)
  let Csg[idx]  = imag(V1#branch)/(2*PI*freq)
  let Csb[idx]  = imag(V4#branch)/(2*PI*freq)
  let Csd[idx]  = imag(V3#branch)/(2*PI*freq)
  let Gsg[idx]  = real(V1#branch)
  let Gss[idx]  = -real(V2#branch)
  let Gsd[idx]  = real(V3#branch)
  let Gsb[idx]  = real(V4#branch)
  let vgs[idx]  = @M1[vgs]
  let ID = ID + delta
  let idx = idx + 1
end

alterparam select = 2
reset

let ID     = start
let idx    = 0

repeat $&num
  alter I1 $&ID
  run
  let Cdd[idx]  = -imag(V3#branch)/(2*PI*freq)
  let Cdg[idx]  = imag(V1#branch)/(2*PI*freq)
  let Cdb[idx]  = imag(V4#branch)/(2*PI*freq)
  let Cds[idx]  = imag(V2#branch)/(2*PI*freq)
  let Gdg[idx]  = real(V1#branch)
  let Gds[idx]  = real(V2#branch)
  let Gdd[idx]  = -real(V3#branch)
  let Gdb[idx]  = real(V4#branch)
  let vgs[idx]  = @M1[vgs]
  let ID = ID + delta
  let idx = idx + 1
end

alterparam select = 3
reset

let ID     = start
let idx    = 0

repeat $&num
  alter I1 $&ID
  run
  let Cbb[idx]  = -imag(V4#branch)/(2*PI*freq)
  let Cbs[idx]  = imag(V2#branch)/(2*PI*freq)
  let Cbd[idx]  = imag(V3#branch)/(2*PI*freq)
  let Cbg[idx]  = imag(V1#branch)/(2*PI*freq)
  let Gbg[idx]  = real(V1#branch)
  let Gbs[idx]  = real(V2#branch)
  let Gbd[idx]  = real(V3#branch)
  let Gbb[idx]  = -real(V4#branch)
  let vgs[idx]  = @M1[vgs]
  let ID = ID + delta
  let idx = idx + 1
end

settype current ids
settype voltage vgs
settype capacitance Cgg
settype capacitance Cgs
settype capacitance Cgd
settype capacitance Cgb
settype capacitance Css
settype capacitance Csg
settype capacitance Csb
settype capacitance Csd
settype capacitance Cdd
settype capacitance Cdg
settype capacitance Cdb
settype capacitance Cds
settype capacitance Cbb
settype capacitance Cbs
settype capacitance Cbd
settype capacitance Cbg
settype admittance Ggg
settype admittance Ggs
settype admittance Ggd
settype admittance Ggb
settype admittance Gss
settype admittance Gsg
settype admittance Gsb
settype admittance Gsd
settype admittance Gdd
settype admittance Gdg
settype admittance Gdb
settype admittance Gds
settype admittance Gbb
settype admittance Gbs
settype admittance Gbd
settype admittance Gbg

write MOS_OP.out vgs Cgg Cgs Cgd Cgb Css Csg Csb Csd Cdd Cdg Cdb Cds Cbb Cbs Cbd Cbg Ggg Ggs Ggd Ggb Gss Gsg Gsb Gsd Gdd Gdg Gdb Gds Gbb Gbs Gbd Gbg ids

*plot ids  vs vgs  ylabel Ids
*plot Cgg  vs ids  ylabel Cgg
*plot Cgs  vs ids  ylabel Cgs
*plot Cgd  vs ids  ylabel Cgd
*plot Cgb  vs ids  ylabel Cgb
*plot Css  vs ids  ylabel Css
*plot Csg  vs ids  ylabel Csg
*plot Csb  vs ids  ylabel Csb
*plot Csd  vs ids  ylabel Csd
*plot Cdd  vs ids  ylabel Cdd
*plot Cdg  vs ids  ylabel Cdg
*plot Cdb  vs ids  ylabel Cdb
*plot Cds  vs ids  ylabel Cds
*plot Cbb  vs ids  ylabel Cbb
*plot Cbs  vs ids  ylabel Cbs
*plot Cbd  vs ids  ylabel Cbd
*plot Cbg  vs ids  ylabel Cbg

*plot Ggg  vs ids  ylabel Ggg
*plot Ggs  vs ids  ylabel Ggs
*plot Ggd  vs ids  ylabel Ggd
*plot Ggb  vs ids  ylabel Ggb
*plot Gss  vs ids  ylabel Gss
*plot Gsg  vs ids  ylabel Gsg
*plot Gsb  vs ids  ylabel Gsb
*plot Gsd  vs ids  ylabel Gsd
*plot Gdd  vs ids  ylabel Gdd
*plot Gdg  vs ids  ylabel Gdg
*plot Gdb  vs ids  ylabel Gdb
*plot Gds  vs ids  ylabel Gds
*plot Gbb  vs ids  ylabel Gbb
*plot Gbs  vs ids  ylabel Gbs
*plot Gbd  vs ids  ylabel Gbd
*plot Gbg  vs ids  ylabel Gbg
